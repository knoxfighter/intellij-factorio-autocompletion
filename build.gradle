import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.8.0'
    id 'org.jetbrains.changelog' version '1.3.1'
}

group 'moe.knox.factorio'
version '1.3.5'

sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jsoup:jsoup:1.15.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

ext.getPluginChangelog = { ->
    StringBuilder changelogHtml = new StringBuilder()

    for (item in changelog.getAll().values()) {
        if (item.isUnreleased) {
            continue
        }

        changelogHtml.append("<h3>" + item.version + "</h3>\n")
        changelogHtml.append("<ul>")

        for (section in item.getSections()) {
            String changesType = section.getKey()

            for ( sectionItem in section.getValue() ) {
                changelogHtml.append("<li>[" + changesType + "] " + sectionItem.substring(2) + "</li>\n")
            }
        }

        changelogHtml.append("</ul>\n")
    }

    return changelogHtml.toString()
}

changelog {
    path = "${project.projectDir}/Changelog.md"
    header = "[${-> version.get()}] - ${new SimpleDateFormat("dd.MM.yyyy").format(new Date())}"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = "2022.2"
    plugins = [
        "com.tang:1.3.7.2-IDEA222"
    ]
}

patchPluginXml {
    changeNotes = getPluginChangelog()
    sinceBuild = "222"
    untilBuild = "222.*"
    version = project.version
}

test {
    useJUnitPlatform()
}
